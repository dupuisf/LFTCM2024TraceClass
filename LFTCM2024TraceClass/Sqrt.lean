import LFTCM2024TraceClass.ContinuousLinearMap
import Mathlib.Topology.ContinuousFunction.UniqueCFC

open Algebra in
theorem _root_.Algebra.adjoin_induction'' {R A : Type*} [CommSemiring R] [Semiring A]
    [Algebra R A] {s : Set A} {x : A} (hx : x âˆˆ Algebra.adjoin R s) {p : A â†’ Prop} (mem : âˆ€ (x : A) (h : x âˆˆ s), p x)
    (algebraMap : âˆ€ (r : R), p (algebraMap R A r))
    (add : âˆ€ x âˆˆ adjoin R s, âˆ€ y âˆˆ adjoin R s, p x â†’ p y â†’ p (x + y))
    (mul : âˆ€ x âˆˆ adjoin R s, âˆ€ y âˆˆ adjoin R s, p x â†’ p y â†’ p (x * y))
    : p x := by
  sorry


theorem elementalStarAlgebra.induction_on {R A : Type*} {P : A â†’ Prop} [CommSemiring R] [StarRing R] [TopologicalSpace A]
    [Semiring A] [StarRing A] [TopologicalSemiring A] [ContinuousStar A] [Algebra R A] [StarModule R A] {x y : A}
    (hy : y âˆˆ elementalStarAlgebra R x) (self : P x) (star_self : P (star x))
    (algebraMap : âˆ€ r, P (algebraMap R A r))
    (add : âˆ€ u âˆˆ elementalStarAlgebra R x, âˆ€ v âˆˆ elementalStarAlgebra R x, P u â†’ P v â†’ P (u + v))
    (mul : âˆ€ u âˆˆ elementalStarAlgebra R x, âˆ€ v âˆˆ elementalStarAlgebra R x, P u â†’ P v â†’ P (u * v))
    (h_closure : âˆ€ s : Set A, s âŠ† elementalStarAlgebra R x â†’ (âˆ€ u âˆˆ s, P u) â†’ âˆ€ v âˆˆ closure s, P v) : P y := by
  apply h_closure (StarSubalgebra.adjoin R {x} : Set A) subset_closure (fun y hy â†¦ ?_) y hy
  rw [SetLike.mem_coe, â† StarSubalgebra.mem_toSubalgebra, StarSubalgebra.adjoin_toSubalgebra] at hy
  apply Algebra.adjoin_induction'' hy
  Â· simp only [Set.instInvolutiveStarSet, Set.singleton_union, Set.mem_insert_iff, Set.mem_star,
      Set.mem_singleton_iff, forall_eq_or_imp]
    refine âŸ¨self, fun a ha â†¦ ?_âŸ©
    rw [star_eq_iff_star_eq] at ha
    exact ha â–¸ star_self
  Â· exact algebraMap
  Â· exact fun u hu v hv â†¦ add u (subset_closure hu) v (subset_closure hv)
  Â· exact fun u hu v hv â†¦ mul u (subset_closure hu) v (subset_closure hv)

open scoped NNReal

attribute [fun_prop] NNReal.continuous_sqrt

noncomputable section


variable {A : Type*} [PartialOrder A] [Ring A] [StarOrderedRing A] [TopologicalSpace A]


section OrderPreserving

-- this class exists solely to get a `StarOrderedRing` instance for continuous functions `C(Î±, R)`.
-- actually, the better solution is to pass the `Star` operation as a parameter to
-- `StarOrderedRing` so that it becomes a `Prop` mixin.
class ContinuousStarSqrt (R : Type*) [PartialOrder R] [NonUnitalSemiring R] [StarOrderedRing R]
    [TopologicalSpace R] where
  sqrt : R â†’ R
  continuous_sqrt : ContinuousOn sqrt {x | 0 â‰¤ x}
  starSqrt_mul_self : âˆ€ x : R, 0 â‰¤ x â†’ star (sqrt x) * sqrt x = x

instance ContinuousMap.instStarOrderedRing {X R : Type*} [TopologicalSpace X] [PartialOrder R]
    [NonUnitalRing R] [StarOrderedRing R] [TopologicalSpace R] [ContinuousStar R]
    [TopologicalRing R]  [ContinuousStarSqrt R] : StarOrderedRing C(X, R) :=
  StarOrderedRing.ofNonnegIff' add_le_add_left fun f â†¦ by
    constructor
    Â· intro hf
      let f' : C(X, {x : R | 0 â‰¤ x}) := .mk _ (map_continuous f |>.codRestrict fun x â†¦ by exact hf x)
      let sqrt' : C({x : R | 0 â‰¤ x}, R) := .mk _ ContinuousStarSqrt.continuous_sqrt.restrict
      use sqrt'.comp f'
      ext x
      exact ContinuousStarSqrt.starSqrt_mul_self (f x) (hf x) |>.symm
    Â· rintro âŸ¨f, rflâŸ©
      rw [ContinuousMap.le_def]
      exact fun x â†¦ star_mul_self_nonneg (f x)

instance : ContinuousStarSqrt â„ where
  sqrt := Real.sqrt
  continuous_sqrt := Real.continuous_sqrt.continuousOn
  starSqrt_mul_self _ := Real.mul_self_sqrt

-- I don't know why we have this instance
instance : ContinuousStarSqrt â„â‰¥0 where
  sqrt := NNReal.sqrt
  continuous_sqrt := NNReal.continuous_sqrt.continuousOn
  starSqrt_mul_self _ _ := NNReal.mul_self_sqrt _

open scoped ComplexOrder in
instance : ContinuousStarSqrt â„‚ where
  sqrt := (â†‘) âˆ˜ Real.sqrt âˆ˜ Complex.re
  continuous_sqrt := by fun_prop
  starSqrt_mul_self x hx := by
    simp only [Function.comp_apply, IsROrC.star_def]
    have : x.re = x := Complex.eq_re_of_ofReal_le hx |>.symm
    rw [Complex.conj_ofReal, â† Complex.ofReal_mul, Real.mul_self_sqrt, this]
    rwa [â† Complex.zero_le_real, this]

instance ContinuousMap.instStarOrderedRingNNReal {X : Type*} [TopologicalSpace X] :
    StarOrderedRing C(X, â„â‰¥0) :=
  StarOrderedRing.ofLEIff fun f g â†¦ by
    constructor
    Â· intro hfg
      use .comp âŸ¨NNReal.sqrt, by fun_propâŸ© (g - f)
      ext1 x
      simpa using add_tsub_cancel_of_le (hfg x) |>.symm
    Â· rintro âŸ¨s, rflâŸ©
      exact fun _ â†¦ by simp

section Generic

variable {R : Type*} {p : A â†’ Prop} [PartialOrder R] [CommRing R] [StarOrderedRing R] [MetricSpace R]
variable [TopologicalRing R] [ContinuousStar R] [ContinuousStarSqrt R] [Algebra R A] [StarModule R A]
variable [ContinuousFunctionalCalculus R p]

lemma cfcHom_mono (a : A) (ha : p a) (f g : C(spectrum R a, R)) (hfg : f â‰¤ g) :
    cfcHom ha f â‰¤ cfcHom ha g :=
  OrderHomClass.mono (cfcHom ha) hfg

lemma cfc_mono {f g : R â†’ R} {a : A} (h : âˆ€ x âˆˆ spectrum R a, f x â‰¤ g x)
    (hf : ContinuousOn f (spectrum R a) := by cfc_cont_tac)
    (hg : ContinuousOn g (spectrum R a) := by cfc_cont_tac) :
    cfc a f â‰¤ cfc a g := by
  by_cases ha : p a
  Â· rw [cfc_apply a f, cfc_apply a g]
    exact cfcHom_mono a ha _ _ fun x â†¦ h x.1 x.2
  Â· simp only [cfc_apply_of_not_predicate _ ha, le_rfl]

lemma CFC.nonneg_on_spectrum (f : R â†’ R) (a : A) (h : âˆ€ x âˆˆ spectrum R a, 0 â‰¤ f x) :
    0 â‰¤ cfc a f := by
  by_cases hf : ContinuousOn f (spectrum R a)
  Â· simpa using cfc_mono h
  Â· simp only [cfc_apply_of_not_continuousOn _ hf, le_rfl]

lemma CFC.nonpos_on_spectrum (f : R â†’ R) (a : A) (h : âˆ€ x âˆˆ spectrum R a, f x â‰¤ 0) :
    cfc a f â‰¤ 0 := by
  by_cases hf : ContinuousOn f (spectrum R a)
  Â· simpa using cfc_mono h
  Â· simp only [cfc_apply_of_not_continuousOn _ hf, le_rfl]

end Generic

section NNReal

variable [Algebra â„â‰¥0 A] [ContinuousFunctionalCalculus â„â‰¥0 ((0 : A) â‰¤ Â·)]

lemma cfcHom_mono_nnreal (a : A) (ha : 0 â‰¤ a) (f g : C(spectrum â„â‰¥0 a, â„â‰¥0)) (hfg : f â‰¤ g) :
    cfcHom ha f â‰¤ cfcHom ha g := by
  have := @OrderHomClass.mono (C(spectrum â„â‰¥0 a, â„â‰¥0) â†’â‹†â‚[â„â‰¥0] A) C(spectrum â„â‰¥0 a, â„â‰¥0) A _ _ _
    StarRingHomClass.instOrderHomClass
  exact this (cfcHom ha (R := â„â‰¥0)) hfg

lemma cfc_mono_nnreal {f g : â„â‰¥0 â†’ â„â‰¥0} {a : A} (h : âˆ€ x âˆˆ spectrum â„â‰¥0 a, f x â‰¤ g x)
    (hf : ContinuousOn f (spectrum â„â‰¥0 a) := by cfc_cont_tac)
    (hg : ContinuousOn g (spectrum â„â‰¥0 a) := by cfc_cont_tac) :
    cfc a f â‰¤ cfc a g := by
  by_cases ha : 0 â‰¤ a
  Â· rw [cfc_apply a f, cfc_apply a g]
    exact cfcHom_mono_nnreal a ha _ _ fun x â†¦ h x.1 x.2
  Â· simp only [cfc_apply_of_not_predicate _ ha]
    exact le_rfl

end NNReal

end OrderPreserving

namespace CFC
section NNReal

variable [Algebra â„â‰¥0 A] [ContinuousFunctionalCalculus â„â‰¥0 ((0 : A) â‰¤ Â·)] [UniqueContinuousFunctionalCalculus â„â‰¥0 A]

def sqrt (a : A) : A := cfc a NNReal.sqrt

@[simp]
lemma sqrt_nonneg (a : A) : 0 â‰¤ sqrt a := by
  by_cases h : 0 â‰¤ a
  Â· exact cfc_predicate ..
  Â· rw [sqrt, cfc_apply_of_not_predicate a h]

@[simp]
lemma isSelfAdjoint_sqrt (a : A) : IsSelfAdjoint (sqrt a) :=
  IsSelfAdjoint.of_nonneg <| by simp

-- is there some reaspon this isn't marked `simp` already?
attribute [simp] IsSelfAdjoint.star_eq

lemma star_sqrt (a : A) : star (sqrt a) = sqrt a := by simp

lemma sq_sqrt_of_nonneg {a : A} (ha : 0 â‰¤ a) : sqrt a ^ 2 = a := by
  nth_rw 2 [â† cfc_id â„â‰¥0 a]
  rw [sqrt, â† cfc_pow ..]
  exact cfc_congr a (fun _ _ â†¦ by simp)

@[simp]
lemma sq_sqrt (a : {x : A // 0 â‰¤ x}) : sqrt (a : A) ^ 2 = a :=
  sq_sqrt_of_nonneg a.2

lemma sqrt_sq_of_nonneg {a : A} (ha : 0 â‰¤ a) : sqrt (a ^ 2) = a := by
  rw [sqrt, â† cfc_comp_pow ..]
  simp [cfc_id' â„â‰¥0 a]

@[simp]
lemma sqrt_sq (a : {x : A // 0 â‰¤ x}) : sqrt (a ^ 2 : A) = a :=
  sqrt_sq_of_nonneg a.2

lemma sqrt_mul_self_of_nonneg {a : A} (ha : 0 â‰¤ a) : sqrt (a * a : A) = a := by
  rw [â† pow_two, sqrt_sq_of_nonneg ha]

@[simp]
lemma sqrt_mul_self (a : {x : A // 0 â‰¤ x}) : sqrt (a * a : A) = a :=
  sqrt_mul_self_of_nonneg a.2

def abs (a : A) : A := sqrt (star a * a)

@[simp] -- maybe we don't want this as a `simp` lemma so we don't go hunting for `IsStarNormal` instances?
lemma _root_.IsStarNormal.abs_star (a : A) [IsStarNormal a] : abs (star a) = abs a := by
  rw [abs, abs, star_star, â† star_comm_self']

lemma cfc_congr_nonneg {f : â„â‰¥0 â†’ â„â‰¥0} (g : â„â‰¥0 â†’ â„â‰¥0) {a : A} (hfg : f = g) : cfc a f = cfc a g :=
  cfc_congr a (fun x _ => by simp only [hfg])

@[simp]
lemma nonneg_star_val_eq (a : {x : A // 0 â‰¤ x}) : star (a : A) = a :=
  (IsSelfAdjoint.of_nonneg a.2).star_eq

attribute [simp] star_mul_self_nonneg mul_star_self_nonneg

@[simp]
lemma abs_nonneg (a : A) : 0 â‰¤ abs a := cfc_predicate ..

lemma abs_of_nonneg {a : A} (ha : 0 â‰¤ a) : abs a = a := by
  lift a to {x : A // 0 â‰¤ x} using ha
  simp [abs, â†sq]

@[simp]
lemma abs_val_eq (a : {x : A // 0 â‰¤ x}) : abs (a : A) = a :=
  abs_of_nonneg a.2

@[simp]
lemma abs_abs (a : A) : abs (abs a) = abs a := by
  lift abs a to {x : A // 0 â‰¤ x} using (by simp) with b
  simp

lemma abs_sq (a : A) : (abs a) ^ 2 = star a * a := by
  rw [abs, sq_sqrt_of_nonneg (by simp)]

@[simp]
lemma isSelfAdjoint_abs (a : A) : IsSelfAdjoint (abs a) :=
  IsSelfAdjoint.of_nonneg <| abs_nonneg a

@[simp]
lemma star_abs (a : A) : star (abs a) = abs a :=
  isSelfAdjoint_abs a |>.star_eq

lemma pow_nonneg {a : A} (ha : 0 â‰¤ a) (n : â„•) : 0 â‰¤ a ^ n := by
  induction n with
  | zero => simpa using star_mul_self_nonneg (1 : A)
  | succ n ih => sorry --exact mul_nonneg ha ih

@[simp]
lemma one_nonneg : 0 â‰¤ (1 : A) := by
  simpa using star_mul_self_nonneg (1 : A)

lemma sq_nonneg {a : A} (ha : IsSelfAdjoint a) : 0 â‰¤ a ^ 2 := by
  simpa [ha.star_eq, sq] using star_mul_self_nonneg a

lemma pow_two_mul_nonneg {a : A} (ha : IsSelfAdjoint a) (n : â„•) : 0 â‰¤ a ^ (2 * n) :=
  pow_mul a 2 n â–¸ pow_nonneg (sq_nonneg ha) n

attribute [aesop safe apply] conjugate_nonneg conjugate_nonneg'
attribute [aesop unsafe 70% apply] pow_nonneg add_nonneg
attribute [aesop unsafe 30% apply] sq_nonneg pow_two_mul_nonneg

example (a b c : A) (hb : 0 â‰¤ 1 + b) : 0 â‰¤ star a * (1 + (1 + b)) ^ 2 * a + c * star c + sqrt (abs a) := by
  solve_by_elim (config := {maxDepth := 10})
    [conjugate_nonneg, add_nonneg, pow_nonneg, sq_nonneg, sqrt_nonneg, abs_nonneg, mul_star_self_nonneg, one_nonneg]

-- We'll need to define positive definite elements to deal with negative powers properly
def rpow (r : â„) (a : A) : A := cfc a (NNReal.rpow Â· r)

lemma rpow_eq_pow {n : â„•} {a : A} (ha : 0 â‰¤ a) : rpow n a = a ^ n := by
  rw [rpow, â† cfc_pow_id (R := â„â‰¥0) a n]
  exact cfc_congr_nonneg _ <| by ext; simp

lemma rpow_zero {a : A} (ha : 0 â‰¤ a): rpow 0 a = 1 := by
  have : (0 : â„) = (0 : â„•) := by simp
  simp only [this, rpow_eq_pow ha, pow_zero]

lemma rpow_one {a : A} (ha : 0 â‰¤ a) : rpow 1 a = a := by
  have : (1 : â„) = (1 : â„•) := by simp
  simp only [this, rpow_eq_pow ha, pow_one]

lemma sqrt_eq_rpow {a : A} : sqrt a = rpow (1/2) a := by
  rw [rpow, sqrt]
  refine cfc_congr_nonneg _ ?_
  ext
  simp only [NNReal.sqrt_eq_rpow, one_div, NNReal.rpow_eq_pow, NNReal.coe_rpow]

lemma rpow_add {râ‚ râ‚‚ : â„} {a : A} : rpow (râ‚ + râ‚‚) a = rpow râ‚ a * rpow râ‚‚ a := by
  sorry

lemma rpow_mul {râ‚ râ‚‚ : â„} {a : A} : rpow râ‚ (rpow râ‚‚ a) = rpow (râ‚ * râ‚‚) a := by
  sorry

end NNReal

section Real

variable [Algebra â„ A] [ContinuousFunctionalCalculus â„ (IsSelfAdjoint : A â†’ Prop)]
variable [UniqueContinuousFunctionalCalculus â„ A]

attribute [fun_prop] NNReal.continuous_coe

lemma nonneg_toReal (f : â„ â†’ â„â‰¥0) (a : A) : 0 â‰¤ cfc a (f Â· : â„ â†’ â„) :=
  nonneg_on_spectrum (h := fun _ _ â†¦ by positivity)


end Real

section IsROrC

variable {ğ•œ : Type*} [IsROrC ğ•œ] {p : A â†’ Prop} [Algebra ğ•œ A] [ContinuousFunctionalCalculus ğ•œ p]
variable [UniqueContinuousFunctionalCalculus ğ•œ A] [TopologicalRing A] [ContinuousStar A] [StarModule ğ•œ A]

open Polynomial in
lemma _root_.Polynomial.toContinuousMapOn_X_eq_restrict_id {R : Type*} [Semiring R] [TopologicalSpace R]
    [TopologicalSemiring R] (s : Set R) :
    (X : R[X]).toContinuousMapOn s = .restrict s (.id R) := by
  ext; simp

#find_home! Polynomial.toContinuousMapOn_X_eq_restrict_id
variable (ğ•œ)

theorem cfcHom_range {a : A} (ha : p a) :
    (cfcHom ha (R := ğ•œ)).range = elementalStarAlgebra ğ•œ a := by
  have := UniqueContinuousFunctionalCalculus.compactSpace_spectrum a (R := ğ•œ)
  rw [StarAlgHom.range_eq_map_top, â† polynomialFunctions.starClosure_topologicalClosure, â†
    StarSubalgebra.topologicalClosure_map _ _ (cfcHom_closedEmbedding ha (R := ğ•œ)),
    polynomialFunctions.starClosure_eq_adjoin_X, StarAlgHom.map_adjoin]
  congr
  rw [Set.image_singleton, Polynomial.toContinuousMapOnAlgHom_apply,
    Polynomial.toContinuousMapOn_X_eq_restrict_id, cfcHom_id ha]

theorem cfc_range {a : A} (ha : p a) : Set.range (cfc a (R := ğ•œ)) = (cfcHom ha (R := ğ•œ)).range := by
  ext
  constructor
  Â· rintro âŸ¨f, rflâŸ©
    by_cases hf : ContinuousOn f (spectrum ğ•œ a)
    Â· rw [cfc_apply a f, SetLike.mem_coe]
      exact âŸ¨_, rflâŸ©
    Â· rw [cfc_apply_of_not_continuousOn a hf]
      exact zero_mem _
  Â· rintro âŸ¨f, rflâŸ©
    classical
    let f' (x : ğ•œ) : ğ•œ := if hx : x âˆˆ spectrum ğ•œ a then f âŸ¨x, hxâŸ© else 0
    have hff' : f = Set.restrict (spectrum ğ•œ a) f'  := by ext; simp [f']
    have : ContinuousOn f' (spectrum ğ•œ a) := by
      rw [continuousOn_iff_continuous_restrict]
      exact hff' â–¸ map_continuous f
    use f'
    rw [cfc_apply a f']
    congr!
    exact hff'.symm

variable {ğ•œ}

theorem induction_on {P : A â†’ Prop} {a : A} (ha : p a) (f : C(spectrum ğ•œ a, ğ•œ))
    (self : P a) (star_self : P (star a)) (algebraMap : âˆ€ r : ğ•œ, P (algebraMap ğ•œ A r))
    (add : âˆ€ gâ‚ gâ‚‚ : C(spectrum ğ•œ a, ğ•œ), P (cfcHom ha gâ‚) â†’ P (cfcHom ha gâ‚‚) â†’ P (cfcHom ha (gâ‚ + gâ‚‚)))
    (mul : âˆ€ gâ‚ gâ‚‚ : C(spectrum ğ•œ a, ğ•œ), P (cfcHom ha gâ‚) â†’ P (cfcHom ha gâ‚‚) â†’ P (cfcHom ha (gâ‚ * gâ‚‚) : A))
    (closure : âˆ€ s : Set C(spectrum ğ•œ a, ğ•œ), (âˆ€ g âˆˆ s, P (cfcHom ha g)) â†’ âˆ€ g' âˆˆ closure s, P (cfcHom ha g')) :
    P (cfcHom ha f) := by
  have hf : cfcHom ha f âˆˆ elementalStarAlgebra ğ•œ a := cfcHom_range ğ•œ ha â–¸ Set.mem_range_self _
  apply elementalStarAlgebra.induction_on hf self star_self algebraMap
  all_goals simp only [â† cfcHom_range ğ•œ ha]
  Â· rintro - âŸ¨f, rflâŸ© - âŸ¨g, rflâŸ© hf hg
    simpa using add f g hf hg
  Â· rintro - âŸ¨f, rflâŸ© - âŸ¨g, rflâŸ© hf hg
    simpa using mul f g hf hg
  Â· show âˆ€ s âŠ† Set.range (cfcHom ha), _
    simpa only [Set.forall_subset_range_iff, Set.forall_mem_image,
      (cfcHom_closedEmbedding ha).closure_image_eq] using closure

@[fun_prop]
lemma cfcHom_continuous {a : A} (ha : p a) : Continuous (cfcHom ha : C(spectrum ğ•œ a, ğ•œ) â†’â‹†â‚[ğ•œ] A) :=
  cfcHom_closedEmbedding ha |>.continuous

theorem comm_cfcHom [T2Space A] {a b : A} (ha : p a) (hbâ‚ : Commute a b)
    (hbâ‚‚ : Commute (star a) b) (f : C(spectrum ğ•œ a, ğ•œ)) :
    Commute (cfcHom ha f) b := by
  apply induction_on (P := fun x â†¦ Commute x b) ha f hbâ‚ hbâ‚‚ (Algebra.commute_algebraMap_left Â· _)
  exact fun _ _ â†¦ by simpa using Commute.add_left
  exact fun _ _ â†¦ by simpa using Commute.mul_left
  intro s hs g hg
  have : s.EqOn (cfcHom ha Â· * b) (b * cfcHom ha Â·) := hs
  refine this.closure ?_ ?_ hg
  all_goals fun_prop

theorem comm_cfcHom_of_isSelfAdjoint [T2Space A] (a b : A) (ha : p a)
    (ha' : IsSelfAdjoint a) (hb : Commute a b) (f : C(spectrum ğ•œ a, ğ•œ)) :
    Commute (cfcHom ha f) b :=
  comm_cfcHom ha hb (ha'.star_eq.symm â–¸ hb) f

-- this is useful so we don't have to do so many stupid case splits.
lemma cfc_cases (P : A â†’ Prop) (a : A) (f : ğ•œ â†’ ğ•œ) (hâ‚€ : P 0)
    (haf : (hf : ContinuousOn f (spectrum ğ•œ a)) â†’ (ha : p a) â†’ P (cfcHom ha âŸ¨_, hf.restrictâŸ©)) :
    P (cfc a f) := by
  by_cases h : p a âˆ§ ContinuousOn f (spectrum ğ•œ a)
  Â· rw [cfc_apply a f h.1 h.2]
    exact haf h.2 h.1
  Â· simp only [not_and_or] at h
    obtain (h | h) := h
    Â· rwa [cfc_apply_of_not_predicate _ h]
    Â· rwa [cfc_apply_of_not_continuousOn _ h]

theorem comm_cfc [T2Space A] {a b : A} (hbâ‚ : Commute a b)
    (hbâ‚‚ : Commute (star a) b) (f : ğ•œ â†’ ğ•œ) : Commute (cfc a f) b :=
  cfc_cases (fun x â†¦ Commute x b) a f (Commute.zero_left _) fun hf ha â†¦ comm_cfcHom ha hbâ‚ hbâ‚‚ âŸ¨_, hf.restrictâŸ©

theorem comm_cfc_of_isSelfAdjoint [T2Space A] {a b : A} (ha : IsSelfAdjoint a) (hbâ‚ : Commute a b)
    (f : ğ•œ â†’ ğ•œ) : Commute (cfc a f) b :=
  comm_cfc hbâ‚ (ha.star_eq.symm â–¸ hbâ‚) f



end IsROrC

section Complex

variable [Algebra â„‚ A] [ContinuousFunctionalCalculus â„‚ (IsStarNormal : A â†’ Prop)]
variable [UniqueContinuousFunctionalCalculus â„‚ A]

lemma isSelfAdjoint_of_coe_real (f : â„‚ â†’ â„) (a : A) : IsSelfAdjoint (cfc a (f Â· : â„‚ â†’ â„‚)) := by
  rw [isSelfAdjoint_iff, â† cfc_star]
  exact cfc_congr a fun x _ â†¦ Complex.conj_ofReal _

open ComplexOrder in
lemma nonneg_coe_nnreal (f : â„‚ â†’ â„â‰¥0) (a : A) : 0 â‰¤ cfc a (f Â· : â„‚ â†’ â„‚) :=
  nonneg_on_spectrum (h := fun _ _ â†¦ by positivity)

end Complex

end CFC

end
